***********************************************************************
* 				DIAC PSpice Models                    *
***********************************************************************
.SUBCKT DIAC DIAC_IN DIAC_OUT PARAMS:
+Tr=0.342
+Delta_V=19V
+Ibo=15uA
*
*Tr:      Rise time (in µs)
*Delta_V: Dynamic breakover voltage
*Ibo:     Breakover current
C_C1         N02098 DIAC_OUT  1u IC=0 
V_IDIAC1         DIAC_IN N01041 DC 0Vdc AC 0Vac 
R_R1         N02098 TRG  {1.462*Tr}
D_D1         N06161 N01041 DZ19V 
E_ABM1         TRG DIAC_OUT VALUE { IF(ABS(I(V_IDIAC1))>{Ibo},1,0)}
D_D2         N06161 N01060 DZ19V 
D_D3         N10655 N01060 DZ14V 
D_D4         N10655 DIAC_OUT DZ14V 
S_S1         N01041 N01060 N02098 DIAC_OUT _S1
RS_S1        N02098 DIAC_OUT 1G
C_C2         DIAC_IN DIAC_OUT  10p
****************
* Switch Model *
****************
.MODEL        _S1 VSWITCH Roff=1e7 Ron=2.2 Voff=0.1V Von=0.99V
****************
* Diodes Model *
****************
.model DZ14V  	D(Is=3.142f Rs=0.1 Ikf=0 N=1 Xti=3 Eg=1.11 M=.3282
+		Vj=.75 Fc=.5 Isr=1.973n Nr=2 Bv=13.6 Ibv=.14467 Nbv=1.093
+		Ibvl=.1m Nbvl=1.2722 Tbv1=001433.3u)
*		Vz = 14 @ 17mA, Zz = 32 @ 1mA, Zz = 8.25 @ 5mA, Zz = 5.75 @ 20mA
.model DZ19V D(Is=6.994f Rs=5.612 Ikf=0 N=1 Eg=1.11 M=.2906
+	     Vj=.75 Fc=.5 Isr=2.088n Nr=2 Bv={Delta_V} Ibv=.17098 Nbv=1.2072
+	     Ibvl=2.002m Nbvl=1.1457 Tbv1=888.89u)
*	Vz = 18 @ 14mA, Zz = 37 @ 1mA, Zz = 11 @ 5mA, Zz = 7.9 @ 20mA
.ends   
*
*********************************************************************
* 				Standard DIACs			    *
*********************************************************************
*
*$

.subckt DB3TG DIAC_IN DIAC_OUT
X1 DIAC_IN DIAC_OUT DIAC params:
+Tr=0.342
+Delta_V=19V
+Ibo=15uA
* 2000 / ST / Rev 0
.ends
*$


.SUBCKT DIAC32 1 2
RS 1 N006 10
RV N002 N004 510k
R1 N006 N002 10k
R2 N004 2 10k
Q4 N002 N005 2 0 PNP
Q2 N004 N003 N006 0 PNP
Q1 N003 N004 2 0 NPN
Q3 N005 N002 N006 0 NPN
D5 N002 N005 D
D6 N004 N003 D
.model NPN NPN(BF=20 TF=5u)
.model PNP PNP(BF=2 TF=5u)
.model D D
.ENDS
